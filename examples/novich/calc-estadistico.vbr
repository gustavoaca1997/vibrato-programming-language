*/
    Cálculo de estadísticos básicos
/*

track varianza(estadisticos: Sample<Melody<32th> >, cantElem: quarter): 32th{
    s: 32th <-> 0 |
    media: 32th <-> play promedio with (estadisticos, cantElem) |
    loop x {
        s <-> s + ((estadisticos![x] - media) ^ 2) |
    } in (cantElem)

    s / cantElem ||
}

track desv_standar(estadisticos: Sample<Melody<32th> >, cantElem: quarter): 32th{
    play sqrt with (play varianza with (estadisticos, cantElem), 0.01) ||
}

track max(estadisticos: Sample<Melody<32th> >, cantElem: quarter): 32th{
    max: 32th <-> estadisticos![0] |
    loop x {
        if( max < estadisticos![x]){
            max <-> estadisticos![x] |
        }
    } in (1,cantElem)
    max ||
}

track min(estadisticos: Sample<Melody<32th> >, cantElem: quarter): 32th{
    min: 32th <-> estadisticos![0] |
    loop x {
        if( min > estadisticos![x]){
            min <-> estadisticos![x] |
        }
    } in (1,cantElem)
    min ||
}
track promedio(estadisticos: Sample<Melody<32th> >, cantElem: quarter): 32th {
    promedio: 32th <-> 0 |
    loop x {
        promedio <-> promedio + estadisticos![x] |
    } in (cantElem)
    promedio / cantElem ||
}

track abs(n:32th): 32th {
    if( n < 0 ) n * -1 ||
    n ||
}

track sqrt(n: 32th, error: 32th): 32th {
    prev: 32th <-> 1.0 |
    new_: 32th <-> 0.5 * (1.0 + n) |

    loop (play abs with (new_ - prev) > error ){
        prev <-> new_ |
        new_ <-> 0.5 * (new_ + n/new_) |
    }
    new_ ||
}

moderato(){
    opcion: quarter |
    cantElem: quarter |
    estadisticos: Sample< Melody<32th> > |
    aux: 32th |

    loop(maj){
        |>("¿Desea iniciar un calculo estadistico?\n(1 / 0)") |
        @(opcion) |
        if(opcion = 0) |] 

        |>("Indique el numero de elementos:") |
        @(cantElem) |

        -- Arreglo donde se guardan los datos
        estadisticos <-> new Melody<32th>(cantElem) |

        -- Guardamos los valores ingresados por el usuario
        loop x {
            |>("Ingrese estadistico #", x) |
            @(aux) |
            estadisticos![x] <-> aux |
        } in (cantElem)

        |>("Cantidad de muestras:", cantElem) |
        |>("Promedio de las muestras: ", play promedio with (estadisticos, cantElem)) |
        |>("Varianza: ", play varianza with (estadisticos, cantElem)) |
        |>("Desviacion estandar: ", play desv_standar with (estadisticos, cantElem)) |
        |>("Muestra Maxima: ", play max with (estadisticos, cantElem)) |
        |>("Muestra Minima: ", play min with (estadisticos, cantElem)) |
    }

}