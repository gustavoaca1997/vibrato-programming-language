track print(arr: Sample<Melody<quarter> >) {
    tamArr: eighth <-> length(arr!)|
    loop x {
        |>(arr![x], ' ') |
    } in (tamArr)
    |>('\n')|
}

track swap(arr: Sample< Melody<quarter> >, pos1: quarter, pos2: quarter){
    aux: quarter <-> arr![pos1] |
    arr![pos1] <-> arr![pos2] |
    arr![pos2] <-> aux |
}

track partition(arr: Sample< Melody<quarter> >, low: quarter, high: quarter): quarter{
    --|>('\t', '\t', "partition -> low: ", low, ", high: ", high, '\n')|
    pivot: quarter <-> arr![high] |
    i: quarter <-> low - 1 |
    loop j {
        if (arr![j] < pivot){
            i <-> i + 1 |
            play swap with (arr, i, j) |
        }
    } in (low, high)
    play swap with (arr, i + 1, high) |
    ( i + 1 ) ||
}


track quicksort(arr: Sample< Melody<quarter> >, low: quarter , high: quarter ){
    |>('\t', "quicksort -> low: ", low, ", high: ", high, ", arr: ", '\t', '\t')|
    play print with (arr)|
    pi : quarter |
    if( low <= high ){
        pi <-> play partition with (arr, low, high) |

        play quicksort with ( arr, low, pi - 1 ) |
        play quicksort with ( arr, pi + 1, high ) |
    }
}

moderato(){
    tamArr: quarter |    
    loop (maj){
        |>("Indique el tama√±o (Ingrese -1 para salir): ")|
        @(tamArr) |
        if(tamArr = -1) |] 

        arr: Sample< Melody<quarter> > <-> new Melody<quarter>(tamArr) |
        aux: quarter |
        loop x {
            @(aux) |
            arr![x] <-> aux |
        } in ( tamArr )

        play print with (arr)|
        play quicksort with (arr, 0, tamArr-1) |
        |>("El arreglo ordenado es: ")|
        play print with (arr)|
    }

}